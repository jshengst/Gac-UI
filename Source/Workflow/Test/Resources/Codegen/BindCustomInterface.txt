module test;
using test::*;
using system::*;

var s = "";

interface ISummer
{
	func GetSum() : int;
	event SumChanged();
	prop Sum : int {GetSum : SumChanged}
}

interface IAdder : ISummer
{
	func Add(value : int) : void;
}

func Bind(summer : ISummer^) : Subscription^
{
	var subscription = bind(summer.Sum);
	subscription.Open();
	attach(subscription.ValueChanged,
		func (value : object) : void
		{
			s = $"$(s)[$(cast int value)]";
		}
	);
	return subscription;
}

func Execute(adder : IAdder^) : void
{
	var subscription = Bind(adder);
	for(x in range [1, 5])
	{
		adder.Add(x);
	}
	subscription.Close();
}

func main() : string
{
	Execute(new IAdder^
	{
		var sum = 0;

		override func GetSum() : int
		{
			return sum;
		}

		override func Add(value : int) : void
		{
			sum = sum + value;
			SumChanged();
		}
	});
	return s;
}